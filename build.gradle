import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'groovy'
    id 'checkstyle'
    id 'codenarc'
    id 'distribution'
    id 'findbugs'
    id 'jacoco'
    id 'pmd'
    id 'com.github.johnrengelman.shadow' version '1.2.2'
    id 'com.github.kt3k.coveralls' version '2.4.0'
    id 'de.undercouch.download' version '2.1.0'
    id 'nebula.provided-base' version '3.0.3'
}

group = 'io.github.ssoloff'
version = '0.1.0'
sourceCompatibility = '1.7'

ext {
    gradleVersion = '2.10'

    jeditAcceptanceTestVersion = '5.3.0'
    acceptanceTestApplicationInstallDir = file("$buildDir/tmp/jedit/application")
    acceptanceTestApplicationHomeDir = file("$acceptanceTestApplicationInstallDir/usr/share/jedit-$jeditAcceptanceTestVersion")
    acceptanceTestPluginJarsDir = file("$buildDir/tmp/jedit/plugin-jars")
}

configurations {
    acceptanceTestApplication {
        description = 'Application to be installed during acceptance tests'
    }

    acceptanceTestCompile {
        description = 'Acceptance test compile-time dependences'
    }

    acceptanceTestPlugins {
        description = 'Plugins to be installed during acceptance tests'
    }

    acceptanceTestRuntime {
        description = 'Acceptance test runtime dependences'
        extendsFrom acceptanceTestCompile
    }
}

repositories {
    jcenter()
    mavenCentral()

    maven {
        url 'https://dl.bintray.com/ssoloff/maven/'
    }
}

dependencies {
    compile(
        [group: 'com.googlecode.java-diff-utils', name: 'diffutils', version: '1.2.1'],
        [group: 'org.eclipse.jdt', name: 'org.eclipse.jdt.annotation', version: '1.1.0']
    )
    provided(
        [group: 'io.github.ssoloff', name: 'jedit', version: '5.2.0'],
        [group: 'io.github.ssoloff', name: 'jedit-common-controls-plugin', version: '1.7.4'],
        [group: 'io.github.ssoloff', name: 'jedit-dirty-gutter-plugin', version: '0.3b'],
        [group: 'io.github.ssoloff', name: 'jedit-git-plugin', version: '0.7.1']
    )
    testCompile(
        [group: 'com.athaydes', name: 'spock-reports', version: '1.2.8', transitive: false],
        [group: 'org.slf4j', name: 'slf4j-api', version: '1.7.13'],
        [group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.13'],
        [group: 'org.spockframework', name: 'spock-core', version: '1.0-groovy-2.4']
    )
    acceptanceTestCompile(
        [group: 'com.athaydes', name: 'spock-reports', version: '1.2.8', transitive: false],
        [group: 'commons-io', name: 'commons-io', version: '2.4'],
        [group: 'io.github.ssoloff', name: 'jedit-test-framework', version: jeditAcceptanceTestVersion],
        [group: 'org.slf4j', name: 'slf4j-api', version: '1.7.13'],
        [group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.13'],
        [group: 'org.spockframework', name: 'spock-core', version: '1.0-groovy-2.4']
    )
    acceptanceTestApplication(
        [group: 'io.github.ssoloff', name: 'jedit', version: jeditAcceptanceTestVersion, classifier: 'bin', ext: 'tar.gz']
    )
    acceptanceTestPlugins(
        [group: 'io.github.ssoloff', name: 'jedit-common-controls-plugin', version: '1.7.4', classifier: 'bin', ext: 'tar.gz'],
        [group: 'io.github.ssoloff', name: 'jedit-dirty-gutter-plugin', version: '0.3b', classifier: 'bin', ext: 'tar.gz'],
        [group: 'io.github.ssoloff', name: 'jedit-git-plugin', version: '0.7.1', classifier: 'bin', ext: 'tar.gz'],
        [group: 'io.github.ssoloff', name: 'jedit-project-viewer-plugin', version: '3.5.3', classifier: 'bin', ext: 'tar.gz'],
        files("$distsDir/${project.name}-${project.version}-bin.tar.gz")
    )
}

configurations {
    provided.allDependencies.each { dep ->
        shadowJar {
            dependencies {
                exclude(dependency(dep))
            }
        }
    }
}

sourceSets {
    acceptanceTest {
        java.srcDir file('src/acceptance-test/java')
        groovy.srcDir file('src/acceptance-test/groovy')
        resources.srcDir file('src/acceptance-test/resources')
        compileClasspath = configurations.acceptanceTestRuntime
        runtimeClasspath = output + compileClasspath
    }

    integrationTest {
        java.srcDir file('src/integration-test/java')
        groovy.srcDir file('src/integration-test/groovy')
        resources.srcDir file('src/integration-test/resources')
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}

task acceptanceTest(type: Test) {
    description = 'Runs the acceptance tests.'
    group = 'Verification'
    mustRunAfter 'integrationTest'

    testClassesDir = sourceSets.acceptanceTest.output.classesDir
    classpath = sourceSets.acceptanceTest.runtimeClasspath

    systemProperty 'jedit.home', acceptanceTestApplicationHomeDir

    doFirst {
        def acceptanceTestApplicationBinDistTarPath = configurations.acceptanceTestApplication.singleFile.path
        copy {
            from tarTree(acceptanceTestApplicationBinDistTarPath)
            into acceptanceTestApplicationInstallDir
        }

        systemProperty 'io.github.ssoloff.acceptanceTestPluginJarsDir', acceptanceTestPluginJarsDir
        def acceptanceTestPluginBinDistTarPaths = configurations.acceptanceTestPlugins.files*.path
        acceptanceTestPluginBinDistTarPaths.each { path ->
            copy {
                from tarTree(path).files
                into acceptanceTestPluginJarsDir
            }
        }
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'Verification'
    mustRunAfter 'test'

    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

task jacocoAcceptanceTestReport(type: JacocoReport) {
    description = 'Generates code coverage report for the acceptance tests.'
    group = 'Verification'

    classDirectories = zipTree(shadowJar.archivePath).matching {
        include 'io/github/ssoloff/**/*'
    }
    sourceDirectories = files(sourceSets.main.allSource.srcDirs)
    executionData acceptanceTest
}

task jacocoIntegrationTestReport(type: JacocoReport) {
    description = 'Generates code coverage report for the integration tests.'
    group = 'Verification'

    sourceSets sourceSets.main
    executionData integrationTest
}

task jacocoRootReport(type: JacocoReport) {
    description = 'Generates aggregate code coverage report for all tests.'
    group = 'Verification'

    sourceSets sourceSets.main
    executionData test, integrationTest, acceptanceTest

    reports {
        html {
            destination = file("$project.jacoco.reportsDir/root/html")
            enabled = true
        }
        xml {
            destination = file("$project.jacoco.reportsDir/root/jacocoRootReport.xml")
            enabled = true
        }
    }
}

task wrapper(type: Wrapper) {
    description = 'Generates Gradle wrapper files.'
    group = 'Build Setup'

    gradleVersion = project.gradleVersion
}

tasks.withType(Test) {
    systemProperty 'com.athaydes.spockframework.report.outputDir', "$reportsDir/spock"
}

acceptanceTest {
    reports.html.destination = file("$reports.html.destination/$name")
    reports.junitXml.destination = file("$reports.junitXml.destination/$name")

    jacocoAcceptanceTestReport.reports.html.destination = file("$project.jacoco.reportsDir/$name/html")
    jacocoAcceptanceTestReport.reports.xml.destination = file("$project.jacoco.reportsDir/$name/jacoco${name.capitalize()}Report.xml")
    jacocoAcceptanceTestReport.reports.html.enabled = true
    jacocoAcceptanceTestReport.reports.xml.enabled = true
}

artifacts {
    archives shadowJar
}

check {
    dependsOn integrationTest, acceptanceTest
}

codenarc {
    configFile = file("${project.projectDir}/config/codenarc/codenarc.groovy")
}

coveralls {
    jacocoReportPath = jacocoRootReport.reports.xml.destination
}

distTar {
    classifier = 'bin'
    compression = Compression.GZIP
    extension = 'tar.gz'
}

distZip {
    classifier = 'bin'
}

distributions {
    main {
        baseName = archivesBaseName
        contents {
            from shadowJar
        }
    }
}

integrationTest {
    reports.html.destination = file("$reports.html.destination/$name")
    reports.junitXml.destination = file("$reports.junitXml.destination/$name")

    jacocoIntegrationTestReport.reports.html.destination = file("$project.jacoco.reportsDir/$name/html")
    jacocoIntegrationTestReport.reports.xml.destination = file("$project.jacoco.reportsDir/$name/jacoco${name.capitalize()}Report.xml")
    jacocoIntegrationTestReport.reports.html.enabled = true
    jacocoIntegrationTestReport.reports.xml.enabled = true
}

pmd {
    ruleSetFiles = files("${project.projectDir}/config/pmd/pmd.xml")
    ruleSets = []
}

processResources {
    filter ReplaceTokens, tokens: [
        'version': project.version
    ]
}

shadowJar {
    exclude 'META-INF/**/*'

    relocate 'difflib', 'myjarjardifflib.difflib'
    relocate 'org.eclipse', 'myjarjareclipse.org.eclipse'
}

test {
    reports.html.destination = file("$reports.html.destination/$name")
    reports.junitXml.destination = file("$reports.junitXml.destination/$name")

    jacocoTestReport.reports.html.enabled = true
    jacocoTestReport.reports.xml.enabled = true
}
