plugins {
    id 'groovy'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.4.0'
    id 'nebula.provided-base' version '3.0.3'
}

version = '0.1.0'
sourceCompatibility = '1.7'

repositories {
    mavenCentral()
}

dependencies {
    provided fileTree(dir: 'libs/jedit', include: '*.jar')
    compile 'com.googlecode.java-diff-utils:diffutils:1.2.1'
    compile 'org.eclipse.jdt:org.eclipse.jdt.annotation:1.1.0'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integTest/java')
        groovy.srcDir file('src/integTest/groovy')
        resources.srcDir file('src/integTest/resources')
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}

task fatJar(type: Jar) {
    classifier = 'all'
    from {
        (configurations.compile - configurations.provided).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    } {
        exclude 'META-INF/**'
    }
    with jar
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'Verification'
    mustRunAfter 'test'

    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

task jacocoIntegrationTestReport(type: JacocoReport) {
    description = 'Generates code coverage report for the integration tests.'
    group = 'Verification'

    sourceSets sourceSets.main
    executionData integrationTest
}

task wrapper(type: Wrapper) {
    description = 'Generates Gradle wrapper files.'
    group = 'Build Setup'

    gradleVersion = '2.10'
}

check {
    dependsOn integrationTest
}

integrationTest {
    reports.html.destination = file("$reports.html.destination/$name")
    reports.junitXml.destination = file("$reports.junitXml.destination/$name")

    jacocoIntegrationTestReport.reports.html.destination = file("$project.jacoco.reportsDir/$name/html")
    jacocoIntegrationTestReport.reports.xml.destination = file("$project.jacoco.reportsDir/$name/jacocoTestReport.xml")
    jacocoIntegrationTestReport.reports.html.enabled = true
    jacocoIntegrationTestReport.reports.xml.enabled = true
}

test {
    reports.html.destination = file("$reports.html.destination/$name")
    reports.junitXml.destination = file("$reports.junitXml.destination/$name")

    jacocoTestReport.reports.html.enabled = true
    jacocoTestReport.reports.xml.enabled = true
}
